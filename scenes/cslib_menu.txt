*bug cslib_menu is a cslib module and must not be accessed directly

*comment --------------------------------------
*comment ###  MENU / CHOICE module: README ###
*comment --------------------------------------
*comment 	This module allows for quick and tidy presentation of complicated
*comment 	"choices" or menus in ChoiceScript.
*comment 
*comment 	Unless otherwise noted, all the routines in this module immediately display/render
*comment 	the menu/choice to the screen, and support and unlimited amount of options, via automatic pagination.
*comment 
*comment # Assumptions
*comment 	In order for this module to work properly, some assumptions must be adhered to:
*comment	- "arrays" are a series of variables that follow the "name_N" pattern, e.g. myvar_1, myvar_2
*comment 	- arrays must start from index 1 (not 0)
*comment	- all arrays must be global (i.e. *create not *temp)
*comment	- all arrays must have a name_count variable that denotes the length of the array, inclusive (e.g. myvar_count)
*comment	- an array must not have any gaps: e.g. myvar_1, my_var2, my_var4 (with no myvar_3)
*comment	- "prefix" (or array ref) values should not include the underscore: '_' (i.e. "myvar" is the prefix, not "myvar_")
*comment 


*comment BUILD_ARRAY
*comment ------------------------------
*comment Renders a choice menu from a given array.
*comment 
*comment 	Example usage:
*comment		*create myvar_1 "Option 1"
*comment		*create myvar_2 "Option 2"
*comment		...
*comment		*create myvar_len 5
*comment		*gosub_scene cslib_menu build_array "myvar"
*comment ------------------------------
*comment 	params:
*comment		p_prefix(string): the array name/reference (prefix)
*comment		p_cancel(boolean): include a 'cancel' option
*comment	returns: 
*comment		(number): a positive number indicating the selected option (or 0, if cancel is selected)
*label build_array
*params p_prefix p_cancel
*gosub _build_array_common p_prefix p_cancel "" "" ""
*gosub _render_choice
*return


*comment BUILD_ARRAY_FILTER
*comment ------------------------------
*comment Renders a choice menu from a given array, but filters out entries
*comment based on a provided filter and filter value.
*comment 
*comment 	Example usage:
*comment		*create item_1 "Sword"
*comment		*create item_type_1 "weapon"
*comment		*create item_2 "Shield"
*comment 		*create item_type_2 "armour"
*comment		...
*comment		*create myvar_count 5
*comment 		Select a weapon:
*comment		*gosub_scene cslib_menu build_array_filter "item" false "type" "MATCH" "weapon"
*comment ------------------------------
*comment 	params:
*comment		p_prefix(string): the array name/reference (prefix)
*comment		p_cancel(boolean): include a 'cancel' option
*comment		p_filter(string): the array attribute to test against
*comment		p_filter_type(string): one of "MATCH" or "NOT_MATCH" - determines filter comparison operation
*comment		p_filter_value(any): the value to check against the filter attribute
*comment	returns: 
*comment		(number): a positive number indicating the selected option (or 0, if cancel is selected)
*label build_array_filter
*params p_prefix p_cancel p_filter p_filter_type p_filter_value
*gosub _build_array_common p_prefix p_cancel p_filter p_filter_type p_filter_value
*gosub _render_choice
*return


*comment BUILD_SIMPLE
*comment ------------------------------
*comment Renders a choice menu containing the given strings as individual options.
*comment 
*comment 	Example usage:
*comment		*gosub_scene cslib_menu build_simple "Option 1" "Option 2" "Option 3" ...
*comment ------------------------------
*comment 	params:
*comment		param_n(string): a series of menu options
*comment	returns: 
*comment		(number): a positive number indicating the selected option
*label build_simple
*params
*temp allow_cancel false
*gosub _build_simple_common
*gosub _render_choice
*return


*comment BUILD_SIMPLE_CANCEL
*comment ------------------------------
*comment Renders a choice menu containing the given strings as individual options
*comment in addition to a cancellation option.
*comment 
*comment 	Example usage:
*comment		*gosub_scene cslib_menu build_simple_cancel "Option 1" "Option 2" "Option 3" ...
*comment ------------------------------
*comment 	params:
*comment		param_n(string): a series of menu options
*comment	returns: 
*comment		(number): a positive number indicating the selected option, or 0 (if cancel was selected)
*label build_simple_with_cancel
*params
*temp allow_cancel true
*gosub _build_simple_common
*gosub _render_choice
*return


*comment ::: INTERNAL FUNCTIONS BELOW (DO NOT CALL DIRECTLY!) :::
*label _build_simple_common
*gosub _init_state
*temp prefix "param_"
*temp filter ""
*temp filter_value ""
*temp page_count "@{(param_count <= const_items_per_page) 1|${(param_count / const_items_per_page)}}"
*if ((page_count modulo 1) > 0)
	*set page_count ((page_count - (page_count modulo 1)) + 1)
*gosub _populate_page
*return


*label _build_array_common
*params p_prefix p_cancel p_filter p_filter_type p_filter_value
*gosub _init_state
*temp prefix p_prefix&"_"
*temp filter p_filter
*temp filter_value p_filter_value
*temp filter_type p_filter_type
*if (filter != "") and ((filter_type != "MATCH") and (filter_type != "NOT_MATCH"))
	*bug filter_type was "${filter_type}" but should be "MATCH" or "NOT_MATCH"
*temp allow_cancel p_cancel
*gosub _reset_filter_and_populate_page
*return


*label _init_state
*comment determines the next/prev option position for smoother navigation
*temp next_pref true
*comment determines the direction in which options are displayed
*temp const_invert_order false
*comment the max amount of options to display per 'page'
*temp const_items_per_page 5
*if (const_items_per_page > 10)
	*bug cslib_menu._init_state: const_items_per_page cannot be greater than 10
*comment the text displayed on the cancel/return button
*temp const_cancel_text "[return]"

*temp cur_page 1
*return

*label _reset_filter_and_populate_page
*comment (re-)calculate the visible options (after a page navigation)
*temp option_slot_id_1 0
*temp option_slot_id_2 0
*temp option_slot_id_3 0
*temp option_slot_id_4 0
*temp option_slot_id_5 0
*temp option_slot_id_6 0
*temp option_slot_id_7 0
*temp option_slot_id_8 0
*temp option_slot_id_9 0
*temp option_slot_id_10 0

*temp total_item_count 0
*temp cur_option_index 1
*temp cur_item_index "@{const_invert_order ${{prefix&\"count\"}}|1}"
*temp items_off_page ((cur_page - 1) * const_items_per_page)

*label filter_loop
*if (filter != "")
	*gosub _apply_filter {((prefix)&(filter&"_"))&cur_item_index} filter_value
	*if not(cslib_ret)
		*gosub _add_item
*else
	*gosub _add_item
*if (items_off_page > 0)
	*set items_off_page -1
*gosub _next_item
*if (cslib_ret)
	*goto filter_loop

*gosub _set_page_count
*gosub _populate_page
*return

*label _populate_page
*comment populate the choice items and check the guards
*temp page_item_count 0
*temp slot 1
*gosub _init_option_vars
*label _populate_page_loop
*if (slot <= const_items_per_page)
	*if (prefix = "param_")
		*comment special handling for simple menus
		*temp offset slot+(const_items_per_page * (cur_page -1))
		*set option_guard[slot] ({prefix&"count"} >= offset) and (const_items_per_page >= slot)
		*if (option_guard[slot])
			*set page_item_count + 1
			*set option_text[slot] "${param[offset]}"
			*set option_ret_val[slot] offset
	*else
		*set option_guard[slot] ((option_slot_id[slot] > 0) and (const_items_per_page >= slot))
		*if (option_guard[slot])
			*set page_item_count + 1
			*set option_text[slot] "${{(prefix)&{\"option_slot_id_\"&slot}}}"
			*set option_ret_val[slot] option_slot_id[slot]
	*set slot + 1
	*goto _populate_page_loop
*comment catch 'no selectable options' errors
*if (page_item_count = 0) and not(allow_cancel)
	*bug cslib_menu._reset_filter: menu construction failed as there were 0 matching items â€” consider allowing cancellation
*return

*label _render_choice
*comment render the choice to the screen
*temp set_page "@{(prefix=\"param_\") _populate_page|_reset_filter_and_populate_page}"
*fake_choice
	*if (page_count > 1)
		*comment when there is more than one page, display a page indicator
		*selectable_if (false) # Page (${cur_page} / ${page_count})
			*bug cslib_menu._render_choice: this option should not be selectable
	*if (((cur_page < page_count) and (next_pref)) or ((cur_page = 1) and (page_count > 1)))
		#[i]Next -->[/i]
			*set next_pref true
			*set cur_page + 1
			*gosub {set_page}
			*goto _render_choice
	*if ((cur_page > 1) and ((cur_page = page_count) or (next_pref != true)))
		#[i]<-- Previous[/i]
			*set next_pref false
			*set cur_page - 1
			*gosub {set_page}
			*goto _render_choice
	*if (option_guard_1)
		#${option_text_1}
			*set cslib_ret option_ret_val_1
			*goto _return
	*if (option_guard_2)
		#${option_text_2}
			*set cslib_ret option_ret_val_2
			*goto _return
	*if (option_guard_3)
		#${option_text_3}
			*set cslib_ret option_ret_val_3
			*goto _return
	*if (option_guard_4)
		#${option_text_4}
			*set cslib_ret option_ret_val_4
			*goto _return
	*if (option_guard_5)
		#${option_text_5}
			*set cslib_ret option_ret_val_5
			*goto _return
	*if (option_guard_6)
		#${option_text_6}
			*set cslib_ret option_ret_val_6
			*goto _return
	*if (option_guard_7)
		#${option_text_7}
			*set cslib_ret option_ret_val_7
			*goto _return
	*if (option_guard_8)
		#${option_text_8}
			*set cslib_ret option_ret_val_8
			*goto _return
	*if (option_guard_9)
		#${option_text_9}
			*set cslib_ret option_ret_val_9
			*goto _return
	*if (option_guard_10)
		#${option_text_10}
			*set cslib_ret option_ret_val_10
			*goto _return
	*if (((cur_page < page_count) and (next_pref != true)) and (cur_page != 1))
		#[i]Next -->[/i]
			*set next_pref true
			*set cur_page + 1
			*gosub {set_page}
			*goto _render_choice
	*if (((cur_page > 1) and (next_pref)) and (cur_page != page_count))
		#[i]<-- Previous[/i]
			*set next_pref false
			*set cur_page - 1
			*gosub {set_page}
			*goto _render_choice
	*if (allow_cancel) #${const_cancel_text}
		*set cslib_ret 0
		*goto _return
*bug cslib_menu._render_choice: unreachable
*label _return
*return


*label _apply_filter
*set cslib_ret false
*if ({((prefix)&(filter&"_"))&cur_item_index} = filter_value)
	*if (filter_type = "NOT_MATCH")
		*set cslib_ret true
*elseif (filter_type != "NOT_MATCH")
	*set cslib_ret true
*return


*label _add_item
*set total_item_count + 1
*if (items_off_page < 1) and (cur_option_index <= const_items_per_page)
	*set option_slot_id[cur_option_index] cur_item_index
	*set cur_option_index + 1
*return


*label _next_item
*temp left "@{const_invert_order ${cur_item_index}|${{prefix&\"count\"}}}"
*temp right "@{const_invert_order 1|${cur_item_index}}"
*set cur_item_index "@{const_invert_order ${(cur_item_index - 1)}|${cur_item_index + 1}}"
*set cslib_ret (left > right)
*return


*label _set_page_count
*comment
*temp page_count 0
*if (total_item_count <= const_items_per_page)
	*set page_count 1
*else
	*set page_count (total_item_count / const_items_per_page)
*if ((page_count modulo 1) > 0)
	*set page_count ((page_count - (page_count modulo 1)) + 1)
*return

*label _init_option_vars
*temp option_guard_1 false
*temp option_guard_2 false
*temp option_guard_3 false
*temp option_guard_4 false
*temp option_guard_5 false
*temp option_guard_6 false
*temp option_guard_7 false
*temp option_guard_8 false
*temp option_guard_9 false
*temp option_guard_10 false
*temp option_text_1 ""
*temp option_text_2 ""
*temp option_text_3 ""
*temp option_text_4 ""
*temp option_text_5 ""
*temp option_text_6 ""
*temp option_text_7 ""
*temp option_text_8 ""
*temp option_text_9 ""
*temp option_text_10 ""
*temp option_ret_val_1 0
*temp option_ret_val_2 0
*temp option_ret_val_3 0
*temp option_ret_val_4 0
*temp option_ret_val_5 0
*temp option_ret_val_6 0
*temp option_ret_val_7 0
*temp option_ret_val_8 0
*temp option_ret_val_9 0
*temp option_ret_val_10 0
*return
